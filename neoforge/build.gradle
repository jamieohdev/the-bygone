plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id "com.modrinth.minotaur" version "2.+"

}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        client2 {
            client()
            programArguments.addAll '--username', 'Dev1'
            gameDirectory = project.file('run2')
        }
        data {
            data()
            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all',
                    '--output', rootProject.project(":common").file('src/generated/resources/').getAbsolutePath(),
                    '--existing', rootProject.project(":common").file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}



dependencies {
    if (project.use_rei.toBoolean()) {
        implementation "me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version"
        implementation "me.shedaniel.cloth:cloth-config-neoforge:$cloth_config_version"
        implementation "dev.architectury:architectury-neoforge:$architectury_version"
    }


    implementation "net.kyrptonaught.customportalapi:cpapireforged-neo-1.21.1:1.0.11"
}



repositories {
    maven {
        url = "https://maven.kyrptonaught.dev"
    }

    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
}



tasks.named('build').configure {
    dependsOn('jarJar')
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") // Remember to have the MODRINTH_TOKEN environment variable set or else this will fail - just make sure it stays private!
    projectId = modrinth_project_id // This can be the project ID or the slug. Either will work!
    versionNumber = "neoforge-$minecraft_version-$version" // You don't need to set this manually. Will fail if Modrinth has this version already
    versionType = "release" // This is the default -- can also be `beta` or `alpha`
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = [minecraft_version] // Must be an array, even with only one version
    loaders = ["neoforge"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    dependencies { // A special DSL for creating dependencies
        // scope.type
        // The scope can be `required`, `optional`, `incompatible`, or `embedded`
        // The type can either be `project` or `version`
        //required.project "fabric-api" // Creates a new required dependency on Fabric API
        //   optional.version "sodium", "mc1.19.3-0.4.8" // Creates a new optional dependency on this specific version of Sodium
    }
}



sourceSets.main.resources { srcDir 'src/generated/resources' }