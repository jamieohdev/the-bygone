plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    compileOnly "net.kyrptonaught.customportalapi:cpapireforged-neo-1.21.1:1.0.11"
    compileOnly "curse.maven:jinxedlib-1203401:6269067"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'
artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    sourceSets.main.resources.sourceDirectories.forEach {
        commonResources it
    }
}